{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Theorem Provers *Are* Refactoring Tools\n",
    "by [Ray Myers](https://www.linkedin.com/in/cadrlife/)\n",
    "\n",
    "This [literate ACL2 notebook](https://github.com/jimwhite/acl2-swf-experiments/blob/main/experiments/refactoring/gilded-rose.ipynb) was converted from https://github.com/raymyers/verified-refactoring\n",
    "\n",
    "**Click this [to watch on YouTube](https://www.youtube.com/watch?v=UdB3XBf219Y):**\n",
    "[![Theorem Provers Are Refactoring Tools](https://img.youtube.com/vi/UdB3XBf219Y/maxresdefault.jpg)](https://www.youtube.com/watch?v=UdB3XBf219Y \"Theorem Provers Are Refactoring Tools\")"
   ]
  },
  {
   "cell_type": "raw",
   "metadata": {},
   "source": [
    ";;; Ray Myers Gilded Rose Simplify Defun Example\n",
    "\n",
    ";;; https://github.com/raymyers/verified-refactoring/blob/main/examples/acl2/simplify-defun/gilded-rose-simplify.lisp\n",
    "\n",
    ";;; Accompanying video: https://youtu.be/UdB3XBf219Y"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Run This Yourself\n",
    "A Docker image for running an [ACL2](https://acl2.org/) kernel in JupyterLab is https://github.com/jimwhite/acl2-jupyter/pkgs/container/acl2-jupyter .\n",
    "That is built by https://github.com/jimwhite/acl2-jupyter .\n",
    "\n",
    "You can run it (for free!) in [GitHub Codespaces](https://docs.github.com/en/codespaces) by clicking this:\n",
    "\n",
    "[![Open in GitHub Codespaces](https://github.com/codespaces/badge.svg)](https://codespaces.new/jimwhite/acl2-swf-experiments)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "__N.B.__\n",
    "If the following fails because the book is *uncertified* then just open a bash terminal (click the `+` in the tabs above then click on `Terminal` in bottom left under `Other`) and run:\n",
    "\n",
    "```bash\n",
    "cert.pl $ACL2_SYSTEM_BOOKS/kestrel/apt/top\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {
    "vscode": {
     "languageId": "acl2"
    }
   },
   "outputs": [
    {
     "execution_count": 1,
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "Summary\n",
      "Form:  ( INCLUDE-BOOK \"kestrel/apt/top\" ...)\n",
      "Rules: NIL\n",
      "Time:  2.44 seconds (prove: 0.00, print: 0.00, other: 2.43)\n",
      " \"/home/acl2/books/kestrel/apt/top.lisp\"\n"
     ]
    }
   ],
   "source": [
    "(include-book \"kestrel/apt/top\" :dir :system)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {
    "vscode": {
     "languageId": "acl2"
    }
   },
   "outputs": [
    {
     "execution_count": 2,
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "Since UPDATE-ITEM is non-recursive, its admission is trivial.  We observe\n",
      "that the type of UPDATE-ITEM is described by the theorem \n",
      "(AND (CONSP (UPDATE-ITEM NAME QUALITY SELL-IN))\n",
      "     (TRUE-LISTP (UPDATE-ITEM NAME QUALITY SELL-IN))).\n",
      "We used primitive type reasoning.\n",
      "\n",
      "Summary\n",
      "Form:  ( DEFUN UPDATE-ITEM ...)\n",
      "Rules: ((:FAKE-RUNE-FOR-TYPE-SET NIL))\n",
      "Time:  0.00 seconds (prove: 0.00, print: 0.00, other: 0.00)\n",
      " UPDATE-ITEM\n"
     ]
    }
   ],
   "source": [
    "(defun update-item (name quality sell-in)\n",
    "    (let ((quality \n",
    "        (if (and (not (equal name \"Aged Brie\"))\n",
    "               (not (equal name \"Backstage passes to a TAFKAL80ETC concert\")))\n",
    "          (if (> quality 0)\n",
    "              (if (not (equal name \"Sulfuras, Hand of Ragnaros\"))\n",
    "                  (- quality 1)\n",
    "                  quality)\n",
    "               quality)\n",
    "           quality)))\n",
    "        (let ((quality \n",
    "                (if (equal name \"Backstage passes to a TAFKAL80ETC concert\")\n",
    "                    (if (< sell-in 11)\n",
    "                        (if (< sell-in 6)\n",
    "                            (if (< quality 50)\n",
    "                                (+ quality 2)\n",
    "                                quality)\n",
    "                            (if (< quality 50)\n",
    "                                (+ quality 1)\n",
    "                                quality))\n",
    "                        quality)\n",
    "                    quality))\n",
    "                (sell-in (if (not (equal name \"Sulfuras, Hand of Ragnaros\"))\n",
    "                    (- sell-in 1)\n",
    "                    sell-in)))\n",
    "            (let ((quality \n",
    "                (if (< sell-in 0)\n",
    "                    (if (not (equal name \"Aged Brie\"))\n",
    "                        (if (not (equal name \"Backstage passes to a TAFKAL80ETC concert\"))\n",
    "                            (if (> quality 0)\n",
    "                                (if (not (equal name \"Sulfuras, Hand of Ragnaros\"))\n",
    "                                    (- quality 1) \n",
    "                                    quality)\n",
    "                                quality)\n",
    "                            0)\n",
    "                        (if (< quality 50)\n",
    "                            (+ quality 1)\n",
    "                            quality))\n",
    "                    quality)))\n",
    "                (list name quality sell-in)))))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {
    "vscode": {
     "languageId": "acl2"
    }
   },
   "outputs": [
    {
     "execution_count": 3,
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "(DEFUN UPDATE-BRIE-SA (NAME QUALITY SELL-IN)\n",
      "  (DECLARE (XARGS :GUARD T :VERIFY-GUARDS NIL))\n",
      "  (LET ((NAME \"Aged Brie\"))\n",
      "    (LET ((SELL-IN (+ -1 SELL-IN)))\n",
      "      (LET ((QUALITY (IF (< SELL-IN 0)\n",
      "                         (IF (NOT (< QUALITY 50))\n",
      "                             QUALITY\n",
      "                           (+ 1 QUALITY))\n",
      "                       QUALITY)))\n",
      "        (LIST NAME QUALITY SELL-IN)))))\n",
      "(DEFTHM UPDATE-ITEM-BECOMES-UPDATE-BRIE-SA\n",
      "  (IMPLIES (EQUAL NAME \"Aged Brie\")\n",
      "           (EQUAL (UPDATE-ITEM NAME QUALITY SELL-IN)\n",
      "                  (UPDATE-BRIE-SA NAME QUALITY SELL-IN))))\n",
      "(DEFUN UPDATE-PASSES-SA (NAME QUALITY SELL-IN)\n",
      "  (DECLARE (XARGS :GUARD T :VERIFY-GUARDS NIL))\n",
      "  (LET ((NAME \"Backstage passes to a TAFKAL80ETC concert\"))\n",
      "    (LET ((QUALITY (IF (< SELL-IN 11)\n",
      "                       (IF (< SELL-IN 6)\n",
      "                           (IF (< QUALITY 50)\n",
      "                               (+ 2 QUALITY)\n",
      "                             QUALITY)\n",
      "                         (IF (< QUALITY 50)\n",
      "                             (+ 1 QUALITY)\n",
      "                           QUALITY))\n",
      "                     QUALITY))\n",
      "          (SELL-IN (+ -1 SELL-IN)))\n",
      "      (LET ((QUALITY (IF (< SELL-IN 0) 0 QUALITY)))\n",
      "        (LIST NAME QUALITY SELL-IN)))))\n",
      "(DEFTHM UPDATE-ITEM-BECOMES-UPDATE-PASSES-SA\n",
      "  (IMPLIES (EQUAL NAME\n",
      "                  \"Backstage passes to a TAFKAL80ETC concert\")\n",
      "           (EQUAL (UPDATE-ITEM NAME QUALITY SELL-IN)\n",
      "                  (UPDATE-PASSES-SA NAME QUALITY SELL-IN))))\n",
      "(DEFUN UPDATE-SULFURAS-SA (NAME QUALITY SELL-IN)\n",
      "  (DECLARE (XARGS :GUARD T :VERIFY-GUARDS NIL))\n",
      "  (LET ((NAME \"Sulfuras, Hand of Ragnaros\"))\n",
      "    (LIST NAME QUALITY SELL-IN)))\n",
      "(DEFTHM UPDATE-ITEM-BECOMES-UPDATE-SULFURAS-SA\n",
      "  (IMPLIES (EQUAL NAME \"Sulfuras, Hand of Ragnaros\")\n",
      "           (EQUAL (UPDATE-ITEM NAME QUALITY SELL-IN)\n",
      "                  (UPDATE-SULFURAS-SA NAME QUALITY SELL-IN))))\n",
      "(DEFUN UPDATE-NORMAL-SA (NAME QUALITY SELL-IN)\n",
      "  (DECLARE (XARGS :GUARD T :VERIFY-GUARDS NIL))\n",
      "  (LET ((QUALITY (IF (< 0 QUALITY)\n",
      "                     (+ -1 QUALITY)\n",
      "                   QUALITY)))\n",
      "    (LET ((SELL-IN (+ -1 SELL-IN)))\n",
      "      (LET ((QUALITY (IF (< SELL-IN 0)\n",
      "                         (IF (NOT (< 0 QUALITY))\n",
      "                             QUALITY\n",
      "                           (+ -1 QUALITY))\n",
      "                       QUALITY)))\n",
      "        (LIST NAME QUALITY SELL-IN)))))\n",
      "(DEFTHM UPDATE-ITEM-BECOMES-UPDATE-NORMAL-SA\n",
      "  (IMPLIES (AND (NOT (EQUAL NAME \"Aged Brie\"))\n",
      "                (NOT (EQUAL NAME\n",
      "                            \"Backstage passes to a TAFKAL80ETC concert\"))\n",
      "                (NOT (EQUAL NAME \"Sulfuras, Hand of Ragnaros\")))\n",
      "           (EQUAL (UPDATE-ITEM NAME QUALITY SELL-IN)\n",
      "                  (UPDATE-NORMAL-SA NAME QUALITY SELL-IN))))\n"
     ]
    }
   ],
   "source": [
    "(simplify-defun update-item :new-name update-brie-sa :assumptions ((equal name \"Aged Brie\")))\n",
    "(simplify-defun update-item :new-name update-passes-sa :assumptions ((equal name \"Backstage passes to a TAFKAL80ETC concert\")))\n",
    "(simplify-defun update-item :new-name update-sulfuras-sa :assumptions ((equal name \"Sulfuras, Hand of Ragnaros\")))\n",
    "(simplify-defun update-item :new-name update-normal-sa \n",
    "    :assumptions \n",
    "    ((not (equal name \"Aged Brie\"))\n",
    "     (not (equal name \"Backstage passes to a TAFKAL80ETC concert\"))\n",
    "     (not (equal name \"Sulfuras, Hand of Ragnaros\"))))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {
    "vscode": {
     "languageId": "acl2"
    }
   },
   "outputs": [
    {
     "execution_count": 4,
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "Since UPDATE-ITEM-REFACTOR-SA is non-recursive, its admission is trivial.\n",
      "We observe that the type of UPDATE-ITEM-REFACTOR-SA is described by\n",
      "the theorem \n",
      "(AND (CONSP (UPDATE-ITEM-REFACTOR-SA NAME QUALITY SELL-IN))\n",
      "     (TRUE-LISTP (UPDATE-ITEM-REFACTOR-SA NAME QUALITY SELL-IN))).\n",
      "We used the :type-prescription rules UPDATE-BRIE-SA, UPDATE-NORMAL-SA,\n",
      "UPDATE-PASSES-SA and UPDATE-SULFURAS-SA.\n",
      "\n",
      "Summary\n",
      "Form:  ( DEFUN UPDATE-ITEM-REFACTOR-SA ...)\n",
      "Rules: ((:TYPE-PRESCRIPTION UPDATE-BRIE-SA)\n",
      "        (:TYPE-PRESCRIPTION UPDATE-NORMAL-SA)\n",
      "        (:TYPE-PRESCRIPTION UPDATE-PASSES-SA)\n",
      "        (:TYPE-PRESCRIPTION UPDATE-SULFURAS-SA))\n",
      "Time:  0.00 seconds (prove: 0.00, print: 0.00, other: 0.00)\n",
      " UPDATE-ITEM-REFACTOR-SA\n"
     ]
    }
   ],
   "source": [
    "(defun update-item-refactor-sa (name quality sell-in)\n",
    "    (cond\n",
    "        ((equal name \"Sulfuras, Hand of Ragnaros\") (update-sulfuras-sa name quality sell-in))\n",
    "        ((equal name \"Aged Brie\") (update-brie-sa name quality sell-in))\n",
    "        ((equal name \"Backstage passes to a TAFKAL80ETC concert\") (update-passes-sa name quality sell-in))\n",
    "        (t (update-normal-sa name quality sell-in))))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {
    "vscode": {
     "languageId": "acl2"
    }
   },
   "outputs": [
    {
     "execution_count": 5,
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "ACL2 Warning [Non-rec] in ( DEFTHM REFACTOR-EQ-SA ...):  A :REWRITE\n",
      "rule generated from REFACTOR-EQ-SA will be triggered only by terms\n",
      "containing the function symbol UPDATE-ITEM, which has a non-recursive\n",
      "definition.  Unless this definition is disabled, this rule is unlikely\n",
      "ever to be used.\n",
      "\n",
      "\n",
      "ACL2 Warning [Subsume] in ( DEFTHM REFACTOR-EQ-SA ...):  A newly proposed\n",
      ":REWRITE rule generated from REFACTOR-EQ-SA probably subsumes the previously\n",
      "added :REWRITE rules UPDATE-ITEM-BECOMES-UPDATE-NORMAL-SA, \n",
      "UPDATE-ITEM-BECOMES-UPDATE-SULFURAS-SA, UPDATE-ITEM-BECOMES-UPDATE-PASSES-SA\n",
      "and UPDATE-ITEM-BECOMES-UPDATE-BRIE-SA, in the sense that the new rule\n",
      "will now probably be applied whenever the old rules would have been.\n",
      "\n",
      "\n",
      "Splitter note (see :DOC splitter) for Goal (9 subgoals).\n",
      "  if-intro: ((:DEFINITION UPDATE-BRIE-SA)\n",
      "             (:DEFINITION UPDATE-ITEM)\n",
      "             (:DEFINITION UPDATE-ITEM-REFACTOR-SA)\n",
      "             (:DEFINITION UPDATE-NORMAL-SA)\n",
      "             (:DEFINITION UPDATE-PASSES-SA))\n",
      "\n",
      "Subgoal 9\n",
      "Subgoal 9'\n",
      "Subgoal 8\n",
      "Subgoal 8'\n",
      "Subgoal 7\n",
      "Subgoal 7'\n",
      "Subgoal 6\n",
      "Subgoal 6'\n",
      "Subgoal 5\n",
      "Subgoal 5'\n",
      "Subgoal 4\n",
      "Subgoal 4'\n",
      "Subgoal 3\n",
      "Subgoal 3'\n",
      "Subgoal 2\n",
      "Subgoal 2'\n",
      "Subgoal 1\n",
      "Subgoal 1'\n",
      "\n",
      "Q.E.D.\n",
      "\n",
      "Summary\n",
      "Form:  ( DEFTHM REFACTOR-EQ-SA ...)\n",
      "Rules: ((:DEFINITION NOT)\n",
      "        (:DEFINITION UPDATE-BRIE-SA)\n",
      "        (:DEFINITION UPDATE-ITEM)\n",
      "        (:DEFINITION UPDATE-ITEM-REFACTOR-SA)\n",
      "        (:DEFINITION UPDATE-NORMAL-SA)\n",
      "        (:DEFINITION UPDATE-PASSES-SA)\n",
      "        (:DEFINITION UPDATE-SULFURAS-SA)\n",
      "        (:EXECUTABLE-COUNTERPART EQUAL)\n",
      "        (:FAKE-RUNE-FOR-TYPE-SET NIL)\n",
      "        (:REWRITE COMMUTATIVITY-OF-+))\n",
      "Splitter rules (see :DOC splitter):\n",
      "  if-intro: ((:DEFINITION UPDATE-BRIE-SA)\n",
      "             (:DEFINITION UPDATE-ITEM)\n",
      "             (:DEFINITION UPDATE-ITEM-REFACTOR-SA)\n",
      "             (:DEFINITION UPDATE-NORMAL-SA)\n",
      "             (:DEFINITION UPDATE-PASSES-SA))\n",
      "Warnings:  Subsume and Non-rec\n",
      "Time:  0.02 seconds (prove: 0.02, print: 0.00, other: 0.00)\n",
      "Prover steps counted:  2292\n",
      " REFACTOR-EQ-SA\n"
     ]
    }
   ],
   "source": [
    "(defthm refactor-eq-sa (equal (update-item a b c) (update-item-refactor-sa a b c)))"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "ACL2",
   "language": "acl2",
   "name": "acl2"
  },
  "language_info": {
   "codemirror_mode": "commonlisp",
   "file_extension": ".lisp",
   "mimetype": "text/x-common-lisp",
   "name": "acl2",
   "pygments_lexer": "common-lisp"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
